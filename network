#!/usr/bin/perl

# Copyright 2016 ARClab, Lionel Riem - https://arclab.ch/
# Copyright 2012 BSDGuides.org (https://github.com/overrider/wireless)
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
#
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.

use strict;
use warnings;


#
# Configuration
#

#my $config_file = "/etc/networks.conf";
my $config_file = "./networks.conf";


#
# Declarations
#

my $version = "VERSION";

sub usage;
sub list;
sub scan;
sub airplane;
sub net_connect;
sub vpn;

sub check_root;
sub dhcp_client;
sub dhcp_setup;
sub dns_setup;
sub ipv4_setup;
sub route_setup;
sub wlan_config;
sub if_lladdr;
sub if_down;
sub vpn_disconnect;
sub vpn_openvpn;


# 
# Check the environment and read the configuration file
#

my %configuration;
my $current;
my $network;
my $vpn;

die("Error: you are not running OpenBSD.\n") unless $^O eq "openbsd";
open FILE,"<",$config_file
    or die("Error: could not open configuration file ($config_file).\n");

while(<FILE>) {
    chomp;
    next if($_ =~ m/^\s*$/);
    next if($_ =~ m/^#/);

    if($_ eq "[config]") {
        $current = "config";
        next;
    }
    if($_ eq "[network]") {
        $current = "network";
        $network += 1;
        next;
    }
    if($_ eq "[vpn]") {
        $current = "vpn";
        $vpn += 1;
        next;
    }
    die("Config error: key=value pair expected on line $..\n")
        unless $_ =~ m/^\w+=.+$/;

    my ($key,$value) = split("=",$_);
    if($current eq "config") {
        $configuration{$current}{$key} = $value;
    } elsif($current eq "network") {
        $configuration{$current}{$network}{$key} = $value;
    } elsif($current eq "vpn") {
        $configuration{$current}{$vpn}{$key} = $value;
    }
}

close(FILE);


#
# Check the configuration and parse commands
#

my %config    = %{$configuration{"config"}};
my %networks  = %{$configuration{"network"}};
my %vpns      = %{$configuration{"vpn"}};
my $wlan_int  = $config{"wlan_int"}
    or die("Config error: could not find the wireless interface.\n");
my $wired_int = $config{"wired_int"};

if(@ARGV) {
    my $command = shift(@ARGV);
    my $argument = shift(@ARGV);

       if(uc($command) eq uc("list"))       { list(); }
    elsif(uc($command) eq uc("scan"))       { scan(); }
    elsif(uc($command) eq uc("airplane"))   { airplane(); }
    elsif(uc($command) eq uc("connect"))    { net_connect($argument); }
    elsif(uc($command) eq uc("vpn"))        { vpn($argument); }
    else                                    { usage(); }
} else {
    usage();
}


#
# Display usage/help
#

sub usage {
    print "Network $version -- Command line network manager\n";
    print "\n";
    print "Usage: network [command] [argument]\n";
    print "\n";
    print "Commands:\n";
    print "   list                List configured networks and VPNs.\n";
    print "   scan                Scan available wireless networks (requires ";
    print                         "root privileges).\n";
    print "                       Warning: scanning disconnects from current ";
    print                         "wireless network!\n";
    print "   airplane            Disable all interfaces and VPNs (requires ";
    print                         "root privileges).\n";
    print "   connect [network]   Connect to configured network (requires ";
    print                         "root privileges).\n";
    print "   vpn [argument]      Connect to a VPN network or, if argument is ";
    print                         "'disconnect',\n";
    print "                       disconnects from a VPN (requires root ";
    print                         "privileges).\n";
    print "   help                Print help (this message).\n";
}


#
# List configured networks
#

sub list {

    # Networks
    print "Configured networks:\n";
    printf("%-10s %-20s %-6s %-20s %-6s %-20s %-20s\n", "Network", "SSID",
        "Type", "IPv4", "DNS", "DHCP hostname", "Link layer addr");
    print "-" x 105 . "\n";
    foreach my $index (sort (keys %networks)) {
        my %net = %{$networks{$index}};
        my $dns;
        if(uc($net{"ipv4"}) eq uc("dhcp") && !defined($net{"dns"})) {
            $dns = "Auto";
        } elsif(!defined($net{"dns"})) {
            $dns = "-";
        } else {
            $dns = "Cust.";
        }
        printf("%-10s %-20s %-6s %-20s %-6s %-20s %-20s\n", $net{"name"},
            ($net{"ssid"} || "-"), $net{"type"}, ($net{"ipv4"} || "dhcp"), $dns,
            ($net{"hostname"} || "-"), ($net{"lladdr"} || "-"));
    }
    print "-" x 105 . "\n\n";

    # VPNs
    print "Configured VPNs:\n";
    printf("%-20s %-10s %-50s\n", "VPN name", "Type", "Configuration file");
    print "-" x 82 . "\n";
    foreach my $index (sort (keys %vpns)) {
        my %vpncon = %{$vpns{$index}};
        my $type = "?";
        if(uc($vpncon{"type"}) eq uc("openvpn")) {
            $type = "OpenVPN";
        }
        printf("%-20s %-10s %-50s\n", $vpncon{"name"}, $type,
            $vpncon{"config"});
    }
    print "-" x 82 . "\n";
}


#
# Scan available wireless networks
#

sub scan {
    my $int = $config{"wlan_int"};
    check_root();

    print "Scanning available wireless networks...\n";
    system("ifconfig $int -nwid -chan down");
    my @result = `ifconfig $int scan`;
    chomp @result;
    my @networks = grep(/chan/, @result);
    printf("%-30s %-5s %-5s %-50s\n", "Network SSID", "Chan", "Sign",
        "Parameters");
    print "-" x 90 . "\n";
    foreach my $line (@networks) {
        $line =~ /^[[:space:]]*nwid (.*) chan (\d+) .* (\d+%) \d+M (.*)$/;
        printf("%-30s %-5s %-5s %-50s\n", $1, $2, $3, $4);
    }
    print "-" x 90 . "\n";
}


#
# Puts the laptop in "airplane" mode by disabling all interfaces
#

sub airplane {
    my $wired_int = $config{"wired_int"};
    my $wlan_int = $config{"wlan_int"};
    check_root();
    if_down($wired_int) if $wired_int;
    if_down($wlan_int);
    print "System now in airplane mode.\n";
}


#
# Connects to a network
#

sub net_connect {
    my $net = shift;
    check_root();

    my $wlan_int = $config{"wlan_int"};
    my $wired_int = $config{"wired_int"};

    my %connect;
    foreach my $index(keys %networks) {
        my %network = %{$networks{$index}};
        if(uc($network{"name"}) eq uc($net)) {
            %connect = %network;
        } else {
            next;
        }
    }

    if(scalar(keys %connect) > 0) {
        my $int;

        # Check if we are using a custom DNS
        my $dns = 0;
        $dns = 1 if $connect{"dns"};

        # Check if ipv4, hostname and lladdr have been provided
        # If not, assume dhcp/default.
        $connect{"ipv4"}     = "dhcp"    unless $connect{"ipv4"};
        $connect{"hostname"} = "default" unless $connect{"hostname"};
        $connect{"lladdr"}   = "default" unless $connect{"lladdr"};

        # Setup the interfaces
        if(uc($connect{"type"}) eq uc("wired")) {
            die("Error: could not find the wired interface.\n")
                unless $wired_int;
            $int = $wired_int;
            if_down($wlan_int);
            if_lladdr($wired_int, $connect{"lladdr"});
        } else {
            $int = $wlan_int;
            if_down($wired_int) if $wired_int;
            if_lladdr($wlan_int, $connect{"lladdr"});
            wlan_config($wlan_int, $connect{"ssid"}, $connect{"key"},
                $connect{"type"});
        }

        # Setup the IP environment
        if(uc($connect{"ipv4"}) eq uc("dhcp")) {
            dhcp_setup($connect{"hostname"}, $dns);
            if($dns == 1) {
                dns_setup($connect{"dns"}, $connect{"dns_domain"});
            }
            dhcp_client($int);
        } else {
            $connect{"ipv4"} =~ /^(([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5])\.){3}([0-9]|[1-9][0-9]|1[0-9]{2}|2[0-4][0-9]|25[0-5])(\/([0-9]|[1-2][0-9]|3[0-2]))$/
                or die("Error: ".$connect{"ipv4"}." is not a valid IPv4 CIDR.\n");
            ipv4_setup($int, $connect{"ipv4"});
            if(defined($connect{"ipv4_gw"})) {
                route_setup($connect{"ipv4_gw"});
            } else {
                print "Warning: you are using a static IPv4 address but ";
                print "haven't set a gateway.\n";
            }
            if($dns == 1) {
                dns_setup($connect{"dns"}, $connect{"dns_domain"});
            } else {
                print "Warning: you are using a static IP address but ";
                print "haven't set DNS settings.\n";
            }
        }

    } else {
        print "Error: network \"$net\" not found in the configuration file.\n";
    }
}


#
# Connect to or disconnect from a VPN
#

sub vpn {
    my $vpncon = shift;
    check_root();

    my %connect;

    # Check if we are disconnecting
    if(uc($vpncon) eq uc("disconnect")) {
        print "Disconnecting VPN.\n";
        vpn_disconnect();
    } else {

        foreach my $index(keys %vpns) {
            my %vpn = %{$vpns{$index}};
            if(uc($vpn{"name"}) eq uc ($vpncon)) {
                %connect = %vpn;
            } else {
                next;
            }
        }
    
        if(scalar(keys %connect) > 0) {
            print "Connecting to \"".$connect{"name"}."\".\n";
            
            # Switch case VPN type
            if(uc($connect{"type"}) eq uc("openvpn")) {
                vpn_openvpn($connect{"config"});
            } else {
                print "Error: unknown VPN type \"".$connect{"type"}."\".\n";
            }
        } else {
            print "Error: VPN \"$vpncon\" not found in the configuration ";
            print "file.\n";
        }
    }
}


#
# Check if running as root or die
#

sub check_root {
    die("Error: this operation requires root privilege.\n") unless $> eq 0;
}


#
# Start DHCP client
#

sub dhcp_client {
    my $interface = shift;
    print "Getting an IP address on $interface via DHCP.\n";
    system("dhclient $interface");
}


#
# Setup /etc/dhclient.conf
#

sub dhcp_setup {
    my $hostname = shift;
    my $dns = shift;
    system("echo '# Generated by network/$version\n' > /etc/dhclient.conf");
    if(uc($hostname) eq uc("default")) {
        $hostname = $config{"hostname"};
    }

    # Setup the hostname
    if(uc($hostname) eq uc("none")) {
        print "Removing DHCP hostname (won't be sent).\n";
        system("echo '#send host-name \"none\";' >> /etc/dhclient.conf");
    } else {
        print "Changing DHCP hostname to \"$hostname\".\n";
        system("echo 'send host-name \"$hostname\";' >> /etc/dhclient.conf");
    }

    # Ignore the DNS, if needed
    if($dns == 1) {
        print "Ignoring DNS settings offered by DHCP.\n";
        system("echo 'ignore domain-name-servers, domain-name;' >> ".
            "/etc/dhclient.conf");
    }
}


#
# Setup /etc/resolv.conf
#

sub dns_setup {
    my $srvlist = shift;
    my $domain  = shift;
    my @servers = split(/ /, $srvlist);
    print "Configuring DNS settings.\n";
    system("echo '# Generated by network/$version\n' > /etc/resolv.conf");
   
    # Go through provided servers
    for my $server (@servers) {
        system("echo 'nameserver $server' >> /etc/resolv.conf");
    }

    # If a domain is provided, add it
    if(defined($domain)) {
        system("echo 'domain $domain' >> /etc/resolv.conf");
        system("echo 'search $domain' >> /etc/resolv.conf");
    }
    
    system("echo 'lookup file bind' >> /etc/resolv.conf");
}


#
# Setup IPv4
#

sub ipv4_setup {
    my $int  = shift;
    my $cidr = shift;
    print "Configuring IPv4 settings.\n";

    system("ifconfig $int inet $cidr");
}

#
# Setup routes
#

sub route_setup {
    my $gw = shift;
    print "Configuring route settings.\n";

    # Flush existing routes
    my $flush = `route -n flush -inet`;

    # Add new gateway
    system("route add -inet default $gw");
}


#
# Set interface link layer address
#

sub if_lladdr {
    my $int    = shift;
    my $lladdr = shift;
    if(uc($lladdr) eq uc("default")) {
        if($int eq $wired_int) {
            $lladdr = $config{"wired_lladdr"};
        } else {
            $lladdr = $config{"wlan_lladdr"};
        }
    }
    die("Error: unable to find default link layer address for $int.\n")
        unless $lladdr;
    print "Setting link layer address for $int to $lladdr.\n";
    system("ifconfig $int lladdr $lladdr");
}


#
# Configure the WLAN interface
#

sub wlan_config {
    my ($int, $ssid, $key, $type) = @_;
    print "Configuring SSID \"$ssid\" on $int.\n";
    system("pkill wpa_supplicant");
    system("ifconfig $int -nwid -nwkey -wpa -wpakey");
    if(uc($type) eq uc("open")) {
        system("ifconfig $int nwid \"$ssid\"");
    } elsif(uc($type) eq uc("wep")) {
        system("ifconfig $int nwid \"$ssid\" nwkey \"$key\"");
    } elsif(uc($type) eq uc("wpa")) {
        system("ifconfig $int nwid \"$ssid\" wpa wpaakms psk wpakey \"$key\"");
    } elsif(uc($type) eq uc("802.1x")) {
        system("ifconfig $int nwid \"$ssid\" wpa wpaakms 802.1x");
        system("wpa_supplicant -D openbsd -i $int -B ".
            "-c /etc/wpa_supplicant.conf");
    } else {
        die("Error: unknown network type \"$type\".\n");
    }
}


#
# Bring interface down
#

sub if_down {
    my $int = shift;
    my $wlan_int = $config{"wlan_int"};
    print "Bringing $int down.\n";
    if($int eq $wlan_int) {
        system("pkill wpa_supplicant");
        system("ifconfig $int -nwid -nwkey -chan -wpa -wpakey down");
    } else {
        system("ifconfig $int down");
    }
    my @result = `ifconfig $int`;
    my @ip = grep(/inet/, @result);
    foreach my $line (@ip) {
        $line =~ /^[[:space:]]*inet (.*) netmask .*$/;
        my $return = `ifconfig $int inet $1 delete`;
    }
}


#
# Disconnect VPN
#

sub vpn_disconnect {
    system("pkill openvpn");
}


#
# OpenVPN handler
#

sub vpn_openvpn {
    my $config = shift;
    system("openvpn --config $config --daemon");
}
